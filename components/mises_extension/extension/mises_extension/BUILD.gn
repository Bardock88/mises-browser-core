import("//mises/components/common/typescript.gni")
import("//build/config/locales.gni")

transpile_web_ui("mises_extension") {
  # Need this to fire re-pack if changed, nevertheless extension is repacked on each 2nd build
  # what is the output bundle called and what is the entry point file
  entry_points = [
    [
      "mises_extension_background",
      rebase_path("background.ts"),
    ],
    [
      "content_element_picker",
      rebase_path("content_element_picker.ts"),
    ],
    [
      "webstore",
      rebase_path("webstore.ts"),
    ],
  ]

  # what is the directory / pack name
  resource_name = "mises_extension"

  # Must match the relative path from the static GRD to the manifest.json
  # plus any other relative path we want these files to live in the extension
  extra_relative_path = "/mises_extension/out"

  # WDP node_modules is not an ancestor of its generated code, which still
  # contains module imports.
  # TODO(petemill): Use already-packed output from WDP, or use webpack for all
  # of it, not just some of it.
  extra_modules =
      [ rebase_path("//mises/vendor/web-discovery-project/node_modules") ]

  public_asset_path = "/out/"

  deps =
      [ "//mises/components/mises_extension:web_discovery_project_resources" ]
}

if (is_mac) {
  group("mises_extension_framework_bundle_data") {
    deps = []
    foreach(locale, platform_pak_locales) {
      deps += [ ":mises_extension_framework_bundle_data_${locale}" ]
    }
  }

  foreach(locale, platform_pak_locales) {
    bundle_data("mises_extension_framework_bundle_data_${locale}") {
      locale = string_replace(locale, "-", "_")
      source_locale = string_replace(locale, "nb", "no")
      sources = [ "//mises/components/mises_extension/extension/mises_extension/_locales/$source_locale/messages.json" ]
      outputs = [ "{{bundle_resources_dir}}/mises_extension/_locales/$locale/{{source_file_part}}" ]
    }
  }
} else {
  group("locales") {
    deps = []
    foreach(locale, platform_pak_locales) {
      deps += [ ":locales_${locale}" ]
    }
  }
  foreach(locale, platform_pak_locales) {
    copy("locales_${locale}") {
      locale = string_replace(locale, "-", "_")
      source_locale = string_replace(locale, "nb", "no")
      sources = [ "//mises/components/mises_extension/extension/mises_extension/_locales/${source_locale}/messages.json" ]
      outputs = [ "$root_out_dir/resources/mises_extension/_locales/${locale}/{{source_file_part}}" ]
    }
  }
}
